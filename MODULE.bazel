"Mull's build system"

module(name = "mull", version = "0.29.0")

bazel_dep(name = "platforms", version = "0.0.11")
bazel_dep(name = "rules_pkg", version = "1.1.0")
bazel_dep(name = "rules_shell", version = "0.4.0")
bazel_dep(name = "aspect_bazel_lib", version = "2.15.3")
bazel_dep(name = "rules_multirun", version = "0.11.0")
bazel_dep(name = "rules_foreign_cc", version = "0.14.0")
bazel_dep(name = "toolchains_llvm", version = "1.5.0")
bazel_dep(name = "rules_cc", version = "0.2.16")
bazel_dep(name = "spdlog", version = "1.15.1")
bazel_dep(name = "reproc", version = "14.2.5")
bazel_dep(name = "sqlite3", version = "3.49.1")
bazel_dep(name = "googletest", version = "1.16.0")
bazel_dep(name = "rules_python", version = "1.3.0")
bazel_dep(name = "bazel_skylib", version = "1.7.1")

TOOLCHAIN_VERSION = "18450561443"
llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm", dev_dependency = True)
llvm.toolchain(
    llvm_version = "19.1.7",
    stdlib = {
        "linux-x86_64": "stdc++",
        "linux-aarch64": "stdc++",
    },
    strip_prefix = {
        "darwin-aarch64": "Darwin-aarch64.toolchain",
        "linux-aarch64": "Linux-aarch64.toolchain",
        "linux-x86_64": "Linux-x86_64.toolchain",
    },
    sha256 = {
        "darwin-aarch64": "23ca5654dff23c2a7d7d14fc27a32eab9467e2a9b0e665654ef7c142a549e093",
        "linux-aarch64": "a45eed3d57bf6199d712f897a616037adcabab53770af6325c92bce0efd3abdb",
        "linux-x86_64": "dc80e312fe8d37e039173ff549d856d1751c4e265e318b87e6c8b877599b95ac",
    },
    urls = {
        "darwin-aarch64": [
            "https://github.com/mull-project/toolchains/releases/download/%s/Darwin-aarch64.toolchain.tar.xz" % TOOLCHAIN_VERSION,
        ],
        "linux-aarch64": [
            "https://github.com/mull-project/toolchains/releases/download/%s/Linux-aarch64.toolchain.tar.xz" % TOOLCHAIN_VERSION,
        ],
        "linux-x86_64": [
            "https://github.com/mull-project/toolchains/releases/download/%s/Linux-x86_64.toolchain.tar.xz" % TOOLCHAIN_VERSION,
        ],
    },
)

use_repo(llvm, "llvm_toolchain")

register_toolchains("@llvm_toolchain//:all")

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "json11",
    integrity = "sha256-bJGfxwlz+J5gZzjkrGT5Grph/r/LIwQA4wv9x9PBszI=",
    urls = ["https://github.com/dropbox/json11/archive/402bcde713fbef0c67276fb09b07ffbe8ee13956.zip"],
    strip_prefix = "json11-402bcde713fbef0c67276fb09b07ffbe8ee13956",
    build_file = ":third_party/json11/json11.BUILD",
)

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(
    python_version = "3.13",
)
use_repo(python, "python_3_13")

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    hub_name = "pypi",
    python_version = "3.13",
    requirements_darwin = "requirements_lock_macos.txt",
    requirements_linux = "requirements_lock_linux.txt",
)

use_repo(pip, "pypi")

mull_supported_llvm_versions = use_extension("//:mull_supported_llvm_versions.bzl", "mull_supported_llvm_versions")
mull_supported_llvm_versions.configure(
    os_version_mapping = {
        "macos": ["16", "17", "18", "19", "20"],
        "ubuntu:22.04": ["13", "14", "15"],
        "ubuntu:24.04": ["14", "15", "16", "17", "18", "19", "20"],
        "rhel:9.6": ["20"],
        "rhel:10.0": ["20"],
    },
)
use_repo(mull_supported_llvm_versions, "supported_llvm_versions")

mull_package_info = use_extension("//:mull_package_info.bzl", "mull_package_info")
use_repo(mull_package_info, "mull_package_info")

SUPPORTED_LLVM_VERSIONS = [
    "13",
    "14",
    "15",
    "16",
    "17",
    "18",
    "19",
    "20",
]

available_llvm_versions = use_extension("//:mull_available_llvm_versions.bzl", "available_llvm_versions")
available_llvm_versions.detect_available()
use_repo(available_llvm_versions, "available_llvm_versions")

mull_deps = use_extension(":mull_deps.bzl", "mull_deps")
mull_deps.configure(versions = SUPPORTED_LLVM_VERSIONS)

[use_repo(mull_deps, "mull_irm_%s" % v) for v in SUPPORTED_LLVM_VERSIONS]
[use_repo(mull_deps, "llvm_%s" % v) for v in SUPPORTED_LLVM_VERSIONS]

http_archive(
    name = "bazel_itertools",
    url = "https://github.com/tillahoffmann/bazel-itertools/archive/refs/tags/0.2.1.tar.gz",
    strip_prefix = "bazel-itertools-0.2.1",
    integrity = "sha256-br2KkRgBtrgmOjrl7Y3nK4cR/tkomEw++udDyw9FbOE=",
)

_ALL_CONTENT = """
filegroup(
    name = "all_srcs",
    srcs = glob(["**"]),
    visibility = ["//visibility:public"],
)
"""

http_archive(
    name = "e2e_test_fmt",
    url = "https://github.com/fmtlib/fmt/archive/123913715afeb8a437e6388b4473fcc4753e1c9a.zip",
    strip_prefix = "fmt-123913715afeb8a437e6388b4473fcc4753e1c9a",
    build_file_content = _ALL_CONTENT,
    patches = [
        ":third_party/fmt/install_test_targets.patch",
    ],
    integrity = "sha256-Ogg56lnV/aNDhZxLt3lAdx17XVQe47zhnIcQRArU/1M=",
)

find_rpmbuild = use_extension("@rules_pkg//toolchains/rpm:rpmbuild_configure.bzl", "find_system_rpmbuild_bzlmod")
use_repo(find_rpmbuild, "rules_pkg_rpmbuild")
register_toolchains("@rules_pkg_rpmbuild//:all")
